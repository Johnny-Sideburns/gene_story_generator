;Header and description
(define (domain redcap2)
;remove requirements that are not needed
(:requirements :typing :conditional-effects :negative-preconditions :strips :disjunctive-preconditions :equality :action-costs :universal-preconditions :conditional-effects)
(:types ;todo: enumerate types and their hierarchy here, e.g. car truck bus - vehicle
    character item location trap - omni
    gift consumable weapon - item
    blade - weapon
)
; un-comment following line if constants are needed
(:predicates ;todo: define predicates here
    (inventory ?item - item ?char - character)
    (whereabouts ?loc - location ?char - character)
    (hate ?char1 - character ?char2 - character)
    (inside ?vict - character ?mon - character)
    (oblivious ?o - location ?char - character)
    (atloc ?o - omni ?loc - location)
    (isdead ?char - character)
    (isasleep ?char - character)
    (issick ?char - character)
    (cangobble ?mon - character)
    (imobile ?char - character)
    (issaved ?vict - character)
    (isweakened ?mon - character)
    (islittlegirl ?char - character)
    (isfull ?char - character)
    (ishungry ?char - character)
    (isheavy ?item - item)
    (poisoned ?char - character)
    (ambushing ?loc - location ?char - character)
    (isset ?trap - trap)
    (isblocked ?loc - location)
)
(:functions ;todo: define numeric functions here
    (total-cost)
)
;define actions here
(:action move
    :parameters (?char - character ?from ?to ?by - location)
    :precondition (and (whereabouts ?from ?char) (not (imobile ?char)) (not (isdead ?char)) (not (isasleep ?char)) (not (oblivious ?to ?char)) (or (and (= ?to ?by) (atloc ?from ?to)) (and (= ?from ?by) (atloc ?to ?from))) (not (isblocked ?to)) (not (isblocked ?from))
    (forall (?chart - character) (or (not (hate ?char ?chart)) (not (whereabouts ?to ?chart)) (isasleep ?chart)))
    )
    :effect (and (not (whereabouts ?from ?char)) (whereabouts ?to ?char)
    (increase (total-cost) 1)
    )
)
(:action pick_up
    :parameters (?char - character ?item - item ?loc - location)
    :precondition (and (whereabouts ?loc ?char) (atloc ?item ?loc) (not (isdead ?char)) (not (isasleep ?char)) (not (cangobble ?char)) (not (issaved ?char)) (not (imobile ?char))
    )
    :effect (and (not (atloc ?item ?loc)) (inventory ?item ?char)
    (increase (total-cost) 1)
    )
)
(:action give
    :parameters (?char1 - character ?char2 - character ?item - consumable ?loc - location)
    :precondition (and (whereabouts ?loc ?char1) (whereabouts ?loc ?char2) (inventory ?item ?char1) (issick ?char2) (not (isdead ?char1)) (not (isasleep ?char1)) (not (hate ?char2 ?char1)) (not (cangobble ?char2))
    )
    :effect (and (not (inventory ?item ?char1)) (inventory ?item ?char2)
    (increase (total-cost) 1)
    )
)
(:action food_coma
    :parameters (?char - character ?loc - location)
    :precondition (and (whereabouts ?loc ?char) (not (isasleep ?char)) (not (isdead ?char)) (isfull ?char)
    (forall (?char1 - character) (or (not (whereabouts ?loc ?char1)) (not (hate ?char ?char1))) )
    )
    :effect (and (isasleep ?char)
    (increase (total-cost) 1)
    )
)
(:action share_info
    :parameters (?char1 - character ?char2 - character ?loc - location ?inf - location)
    :precondition (and (whereabouts ?loc ?char1) (whereabouts ?loc ?char2) (oblivious ?inf ?char1) (not (oblivious ?inf ?char2)) (not (isDead ?char1)) (not (isDead ?char2)) (not (hate ?char1 ?char2))
    (not (isasleep ?char1)) (not (isasleep ?char2))
    )
    :effect (and (not (oblivious ?inf ?char1))
    (increase (total-cost) 1)
    )
)
(:action swallow
    :parameters (?mon - character ?vict - character ?loc ?parloc - location)
    :precondition (and (whereabouts ?loc ?vict) (whereabouts ?loc ?mon) (cangobble ?mon) (not (= ?mon ?vict)) (not (hate ?mon ?vict)) (not (isDead ?mon)) (not (isasleep ?mon)) (not (inside ?vict ?mon)) (not (issaved ?vict)) (not (isweakened ?mon)) (atloc ?loc ?parloc)
    (forall (?char - character) (and  (or (not (whereabouts ?loc ?char)) (= ?char ?vict) (= ?char ?mon)) (or (not (whereabouts ?parloc ?char)) (= ?char ?vict) (= ?char ?mon)))
    )
    )
    :effect (and (inside ?vict ?mon) (imobile ?mon) (not (whereabouts ?loc ?vict))  (when (not (ishungry ?mon)) (isfull ?mon)) (when (ishungry ?mon) (not (ishungry ?mon)))
    (increase (total-cost) 1)
    )
)
(:action cesarean
    :parameters (?char - character ?mon - character ?bab - character ?cut - blade ?loc - location)
    :precondition (and (whereabouts ?loc ?char) (whereabouts ?loc ?mon) (inventory ?cut ?char) (inside ?bab ?mon) (not (= ?char ?mon)) (not (isasleep ?char)) (not (isdead ?char)) (isasleep ?mon)
    (not (isDead ?mon) )
    )
    :effect (and (not (inside ?bab ?mon)) (issaved ?bab) (whereabouts ?loc ?bab) (isweakened ?mon) 
    (when (and (not (isfull ?mon)) (not (ishungry ?mon))) (ishungry ?mon))
    (when (isfull ?mon) (not (isfull ?mon))
    )
    (increase (total-cost) 1)
    )
)
(:action give_gift
    :parameters (?char1 ?char2 - character ?gift - gift ?loc - location)
    :precondition (and (whereabouts ?loc ?char1) (whereabouts ?loc ?char2) (inventory ?gift ?char1) (issick ?char2) (islittlegirl ?char1) (not (isdead ?char1)) (not (isdead ?char2)) (not (isasleep ?char1)) (not (hate ?char1 ?char2))
    )
    :effect (and (not (inventory ?gift ?char1)) (inventory ?gift ?char2)
    (increase (total-cost) 1)
    )
)
(:action stuff
    :parameters (?char1 ?char2 - character ?item - item ?loc - location)
    :precondition (and (whereabouts ?loc ?char1) (whereabouts ?loc ?char2) (atloc ?item ?loc) (isweakened ?char2) (isheavy ?item) (ishungry ?char2)
    )
    :effect (and (poisoned ?char2) (not (atloc ?item ?loc))
    (increase (total-cost) 1)
    )
)
(:action lay_ambush
    :parameters (?mon - character ?loc - location)
    :precondition (and (whereabouts ?loc ?mon) (not (isdead ?mon)) (not (isasleep ?mon)) (not (ambushing ?loc ?mon)) (cangobble ?mon))
    :effect (and (ambushing ?loc ?mon) (imobile ?mon) (isblocked ?loc)
    (increase (total-cost) 1)
    )
)
(:action expire
    :parameters (?char - character ?loc - location)
    :precondition (and (poisoned ?char) (whereabouts ?loc ?char))
    :effect (and (isdead ?char) (when (ambushing ?loc ?char) (and (not (isblocked ?loc)) (not (ambushing ?loc ?char))))
    (increase (total-cost) 1)
    )
)
(:action set_trap
    :parameters (?char ?mon - character ?trap - trap ?bait - consumable ?pois - item ?loc - location)
    :precondition (and (whereabouts ?loc ?char) (atloc ?trap ?loc) (inventory ?bait ?char) (inventory ?pois ?char) (isheavy ?pois) (ambushing ?loc ?mon)
    )
    :effect (and (isset ?trap) (not (inventory ?bait ?char)) (not (inventory ?pois ?char))
    (increase (total-cost) 1)
    )
)
(:action eat_bait
    :parameters (?mon - character ?trap - trap ?loc - location)
    :precondition (and (whereabouts ?loc ?mon) (atloc ?trap ?loc) (isset ?trap) (not (isasleep ?mon)) (not (isdead ?mon)) (not (isweakened ?mon)) (cangobble ?mon)
    )
    :effect (and (isfull ?mon) (not (isset ?trap)) (poisoned ?mon)
    (increase (total-cost) 1)
    )
)
(:action share_food_while_waiting
    :parameters (?char1 ?char2 - character ?item - consumable ?loc - location)
    :precondition (and (issick ?char1) (whereabouts ?loc ?char2) (inventory ?item ?char1) (whereabouts ?loc ?char1) (not (isdead ?char2)) (not (isasleep ?char2)) (not (cangobble ?char1)) (not (cangobble ?char2))
    (forall (?trap - trap) (and (atloc ?trap ?loc) (isset ?trap)))
    )
    :effect (and (not (inventory ?item ?char1)) (not (issick ?char1))
    (increase (total-cost) 1)
    )
)
)